buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url = "https://files.minecraftforge.net/maven"
            name = "Forge"
        }
        maven {
            url = "https://maven.fabricmc.net/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:3.+"
        classpath "net.fabricmc:fabric-loom:0.2.7-SNAPSHOT"
    }
}

apply plugin: 'java'

subprojects {
    apply plugin: 'java-library'
    version = ''

    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

if (release.toUpperCase() != "RELEASE")
    version += "-$release"

description = project.displayname

evaluationDependsOnChildren()

sourceSets {
    old {
//        java.srcDir "oldSrc/main/java"
        resources.srcDir "oldSrc/main/resources"
    }
}

jar {
    subprojects.each {
        dependsOn it.build
//        from zipTree(it.jar.outputs.files.singleFile)
    }
    from(zipTree(project("common").shadowJar.outputs.files.singleFile)) {
        exclude 'fabric.mod.json'
    }
    from zipTree(project("forge").jar.outputs.files.singleFile)
    from zipTree(project("fabric").remapJar.outputs.files.singleFile)

    from 'LICENSE'

    manifest.attributes(
            "Specification-Title": project.name,
            "Specification-Version": project.version,
            "Specification-Vendor": "MineLittlePony",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor": "MineLittlePony",
            "Implementation-Timestamp": new Date()
    )
}
